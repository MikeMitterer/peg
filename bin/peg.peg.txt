Grammar <- SPACING? GLOBALS? Members? Definition+ EOF

Members <- "{" ActionBody* "}" SPACING

Action <- "{" ActionBody* "}" SPACING

ActionBody <- Action / !"}" .

Definition <- IDENTIFIER LEFTARROW Expression

Expression <- Sequence (SLASH Sequence)*

Sequence <- Prefix+

Prefix <- (AND / NOT)? Suffix Action?

Suffix <- Primary (QUESTION / STAR / PLUS)?

Primary <- IDENTIFIER !LEFTARROW / OPEN Expression CLOSE / LITERAL / CLASS / DOT

CHAR <- "\\" ["'\-\[-\]nrt] / HEX_NUMBER / !"\\" !EOL .

CLASS <- "[" (!"]" RANGE)* "]" SPACING

LITERAL <- "\'" (!"\'" CHAR)* "\'" SPACING / "\"" (!"\"" CHAR)* "\"" SPACING

EOF <- !.

GLOBALS <- "%{" GLOBALS_BODY* "}%" SPACING

GLOBALS_BODY <- !"}%" .

IDENTIFIER <- IDENT_START IDENT_CONT* SPACING

IDENT_START <- [A-Z_a-z]

IDENT_CONT <- IDENT_START / [0-9]

LEFTARROW <- "<-" SPACING

RANGE <- CHAR "-" CHAR / CHAR

SLASH <- "/" SPACING

AND <- "&" SPACING

NOT <- "!" SPACING

QUESTION <- "?" SPACING

STAR <- "*" SPACING

PLUS <- "+" SPACING

OPEN <- "(" SPACING

CLOSE <- ")" SPACING

DOT <- "." SPACING

HEX_NUMBER <- [\] "u" [0-9A-Fa-f]+

COMMENT <- "#" (!EOL .)* EOL?

SPACE <- [\t ] / EOL

SPACING <- (SPACE / COMMENT)*

EOL <- "\r\n" / [\n\r]
