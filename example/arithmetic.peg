%{
part of peg.example.arithmetic;

num _binop(num left, num right, String op) {
  switch(op) {
    case "+":
      return left + right;
    case "-":
      return left - right;
    case "*":
      return left * right;
    case "/":
      return left / right;
    default:
      throw "Unsupported operation $op";  
  }
}

}%

Expr <-
  SPACES? Sentence EOF { $$ = $2; }

Sentence <-
  Term (PLUS / MINUS) Sentence { $$ = _binop($1, $3, $2); }
  / Term

Term <-
  Atom (MUL / DIV) Term { $$ = _binop($1, $3, $2); }
  / Atom

Atom <-
  NUMBER
  / OPEN Sentence CLOSE { $$ = $2; }

# Tokens

CLOSE <-
  ')' _SPACES

DIV <-
  '/' _SPACES { $$ = $1; }

EOF <-
  !.

MINUS <-
  '-' _SPACES { $$ = $1; }

MUL <-
  '*' _SPACES { $$ = $1; }

NUMBER <-
  [0-9]+ _SPACES { $$ = int.parse($1.join()); }

OPEN <-
  '(' _SPACES

PLUS <-
  '+' _SPACES { $$ = $1; }
 
SPACES <-
  _SPACES

 WS <-
   [ \n\t\r] / '\r\n'

_SPACES <-
  WS*